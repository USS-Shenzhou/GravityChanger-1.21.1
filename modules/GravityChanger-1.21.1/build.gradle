plugins {
	id 'fabric-loom' version '1.7.+'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.3.5"
}

var archives_base_name="gravity-changer"
var changer_minecraft_version="1.21.1"
var changer_yarn_mappings="1.21.1+build.3"
var changer_loader_version="0.16.13"
var changer_mod_version="1.3.0"
var changer_archives_base_name="gravity-changer"
var changer_fabric_version="0.115.4+1.21.1"
var changer_modmenu_version="11.0.3"
var changer_cloth_config_version="15.0.140"
var changer_cca_version="6.1.1"

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = changer_archives_base_name
version = "${changer_mod_version}+mc${changer_minecraft_version}"
group = "mortimer-kerman"

repositories {
	maven {
		url "https://maven.shedaniel.me/"
	}
	maven {
		url "https://maven.terraformersmc.com/"
	}
	maven {
		name = 'Ladysnake'
		url = 'https://maven.ladysnake.org/releases'
	}
	maven { url = "https://jitpack.io" }

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}

	// for parchment
	maven {
		url = "https://maven.parchmentmc.org"
	}
}

//tasks.named('remapSourcesJar') {
//	enabled = false
//}

dependencies {
	minecraft "com.mojang:minecraft:${changer_minecraft_version}"

	mappings loom.layered() {
		officialMojangMappings() {
			setNameSyntheticMembers(false)
		}
		parchment("org.parchmentmc.data:parchment-1.19.3:2022.12.18")
	}

	modImplementation "net.fabricmc:fabric-loader:${changer_loader_version}"

	modImplementation("net.fabricmc.fabric-api:fabric-api:${changer_fabric_version}") {
		exclude module: "fabric-gametest-api-v1"
		// for now, the gametest module makes client unable to login dev env server
	}
	modApi("me.shedaniel.cloth:cloth-config-fabric:${changer_cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modImplementation "maven.modrinth:modmenu:${changer_modmenu_version}"

	// Replace modImplementation with modApi if you expose components in your own API
	modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${changer_cca_version}"
	// Includes Cardinal Components API as a Jar-in-Jar dependency (optional but recommended)
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${changer_cca_version}"

	modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${changer_cca_version}"
	// Includes Cardinal Components API as a Jar-in-Jar dependency (optional but recommended)
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${changer_cca_version}"

	modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${changer_cca_version}"
	// Includes Cardinal Components API as a Jar-in-Jar dependency (optional but recommended)
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${changer_cca_version}"

}

processResources {
	inputs.property "version", changer_mod_version

	filesMatching("fabric.mod.json") {
		expand "version": changer_mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifactId = changer_archives_base_name
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.

//		maven {
//			name = "GitHubPackages"
//			url = "https://maven.pkg.github.com/qouteall/GravityChanger"
//			credentials {
//				username = System.getenv("GITHUB_ACTOR")
//				password = System.getenv("GITHUB_TOKEN")
//			}
//		}
	}
}

task printClasspath {
	doLast {
		// NOTE don't forget to exclude the submod dev jar
		configurations.compileClasspath.each { println "\"" + (it.toString().replace("\\", "/")) + "\"," }
	}
}

java {
	withSourcesJar()
}

publishMods {
	file = remapJar.archiveFile
	changelog = file("changelog.md").text
	type = ALPHA
	modLoaders.add("fabric")

	displayName = "v${changer_mod_version}-mc${changer_minecraft_version}"
	version = "v${changer_mod_version}-mc${changer_minecraft_version}"

	modrinth {
		projectId = "bqOyYdKi"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersions.add(minecraft_version)
	}
	github {
		repository = "qouteall/GravityChanger"
		accessToken = providers.environmentVariable("GITHUB_TOKEN_FOR_GRAVITY_CHANGER")
		commitish = minecraft_version // the branch name is the same as minecraft version
	}
}

tasks.named('runClient') {
	onlyIf {
		gradle.startParameter.taskNames.contains(this.path)
	}
}