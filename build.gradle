plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.80'
    id 'org.sinytra.adapter.userdev' version '1.2.1-SNAPSHOT'
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dir 'libs'
    }
    maven {
        name = "Sinytra"
        url = "https://maven.su5ed.dev/releases"
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
    maven {
        name = 'Ladysnake'
        url = 'https://maven.ladysnake.org/releases'
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
            systemProperty 'mixin.debug.export', 'true'
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }


dependencies {
    // Example mod dependency with JEI
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    // compileOnly "mezz.jei:jei-${mc_version}-forge-api:${jei_version}"
    // runtimeOnly "mezz.jei:jei-${mc_version}-forge:${jei_version}"

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation "blank:coolmod-${mc_version}:${coolmod_version}"

    // Example mod dependency using a file as dependency
    // implementation files("libs/coolmod-${mc_version}-${coolmod_version}.jar")

    // Example project dependency using a sister or child project:
    // implementation project(":myproject")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    //implementation("curse.maven:sodium-394468:6382651")

    implementation(jarJar("cn.ussshenzhou:MadParticle:0.8.12+kamu") {
        exclude group: "curse.maven", module: "irisshaders-455508"
    })

    implementation(jarJar("cn.ussshenzhou:t88:0.12.+") {
        exclude group: "curse.maven", module: "irisshaders-455508"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-renderer-api-v1"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-api-base"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-rendering-data-attachment-v1"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-block-view-api-v2"
    })
    annotationProcessor("cn.ussshenzhou:t88:0.12.+") {
        exclude group: "curse.maven", module: "irisshaders-455508"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-renderer-api-v1"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-api-base"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-rendering-data-attachment-v1"
        exclude group: "org.sinytra.forgified-fabric-api", module: "fabric-block-view-api-v2"
    }
    //implementation(jarJar(fileTree(dir: 'libs', include: ['*.jar'])))

    implementation(jarJar("cn.ussshenzhou:section31:0.1.+"))
    additionalRuntimeClasspath "org.sinytra:connector:2.0.0-beta.7+1.21.1"
    // Add FFAPI dependency
    implementation(jarJar("org.sinytra.forgified-fabric-api:forgified-fabric-api:0.+"))
    // Install desired Fabric mods. Make sure they remain unmapped at runtime

    implementation(jarJar("dev.onyxstudios.cardinal-components-api:cardinal-components-base:6.1.1") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc"
    })
    implementation(jarJar("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:6.1.1") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc"
    })
    implementation(jarJar("dev.onyxstudios.cardinal-components-api:cardinal-components-world:6.1.1") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc"
    })
    implementation(jarJar("me.shedaniel.cloth:cloth-config-fabric:15.0.140") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc"
    })
    jarJar(implementation(project(':modules:GravityChanger-1.21.1')) {
        exclude group: "me.shedaniel.cloth", module: "cloth-config-fabric"
        exclude group: "dev.onyxstudios.cardinal-components-api", module: "cardinal-components-base"
        exclude group: "dev.onyxstudios.cardinal-components-api", module: "cardinal-components-entity"
        exclude group: "dev.onyxstudios.cardinal-components-api", module: "cardinal-components-world"
        exclude group: "net.fabricmc", module: "fabric-loader"
        exclude group: "net.fabricmc.fabric-api", module: "fabric-api"
        exclude group: "maven.modrinth", module: "modmenu"
    })

    implementation(jarJar("dev.onyxstudios.cardinal-components-api:cardinal-components-base:6.1.1"){
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    })
    implementation(jarJar("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:6.1.1"){
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    })
    implementation(jarJar("dev.onyxstudios.cardinal-components-api:cardinal-components-world:6.1.1"){
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    })
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

tasks.register("runWithRenderDoc", Exec) {
    def javaExecTask = tasks.withType(JavaExec).named("runClient").get()
    def javaHome = javaExecTask.javaLauncher.get().metadata.installationPath.asFile.absolutePath

    commandLine = [
            "C:\\Program Files\\RenderDoc\\renderdoccmd.exe",
            "capture",
            "--opt-hook-children",
            "--wait-for-exit",
            "--working-dir",
            ".",
            "$javaHome\\bin\\java.exe",
            "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005",
            "-Xmx64m",
            "-Xms64m",
            "-Dorg.gradle.appname=gradlew",
            "-Dorg.gradle.java.home=$javaHome",
            "-classpath",
            "gradle/wrapper/gradle-wrapper.jar",
            "org.gradle.wrapper.GradleWrapperMain",
            "runClient"
    ]
}
